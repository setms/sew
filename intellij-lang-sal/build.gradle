plugins {
    id 'java'
    alias libs.plugins.intellij.grammarkit
    alias libs.plugins.intellij.platform
    alias libs.plugins.spotless
}

repositories {
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity("2024.3.5")
        instrumentationTools()
        pluginVerifier()
    }
}

generateLexer {
    sourceFile = file('src/main/grammar/Sal.flex')
    targetOutputDir = file('build/generated/sources/flex/org/setms/sew/intellij/lang/sal')
    purgeOldFiles = true
}
sourceSets.main.java.srcDir 'build/generated/sources/flex'

generateParser {
    sourceFile = file('src/main/grammar/Sal.bnf')
    targetRootOutputDir = file('build/generated/sources/bnf')
    pathToParser = 'parser'
    pathToPsiRoot = 'psi'
    purgeOldFiles = true
}
sourceSets.main.java.srcDir 'build/generated/sources/bnf'

compileJava.dependsOn generateParser, generateLexer