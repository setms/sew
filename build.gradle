tasks.register('installGitHooks') {
    description = 'Installs git pre-commit hook that runs spotlessApply'
    group = 'git hooks'

    def sourceFile = file('hooks/pre-commit')
    def targetFile = file('.git/hooks/pre-commit')
    def gitDir = file('.git')

    inputs.file(sourceFile)
    outputs.file(targetFile)

    onlyIf {
        gitDir.exists()
    }

    doLast {
        targetFile.parentFile.mkdirs()
        targetFile.text = sourceFile.text
        targetFile.setExecutable(true, false)
        logger.quiet('Git pre-commit hook installed successfully')
    }
}

// Run installGitHooks before any task execution
gradle.projectsEvaluated {
    rootProject.tasks.configureEach { task ->
        if (task != rootProject.tasks.installGitHooks) {
            task.dependsOn(rootProject.tasks.installGitHooks)
        }
    }
}

subprojects {
    apply plugin: 'java'

    group = "org.setms.$project.name"
    version = '0.1.0'

    repositories {
        mavenCentral()
        maven {
            url = uri('https://repo.languagetool.org/artifactory/languagetool-os-release/')
        }
        maven {
            url = uri('https://repo.gradle.org/gradle/libs-releases/')
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // Configure Spotless for all projects that have the plugin applied
    pluginManager.withPlugin('com.diffplug.spotless') {
        spotless {
            java {
                target 'src/**/*.java'
                googleJavaFormat()
            }
        }
    }

}