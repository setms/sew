plugins {
    id 'org.jetbrains.intellij.platform' version '2.6.0'
    id "org.jetbrains.grammarkit" version "2022.3.2.2"
}

group = 'org.setms.sew'
version = '0.1.0'

repositories {
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    implementation project(':core')
    implementation libs.bundles.antlr.intellij

    testImplementation platform('org.junit:junit-bom:5.12.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    intellijPlatform {
        intellijIdeaCommunity("2024.3.5")
        instrumentationTools()
        pluginVerifier()
    }
}

intellijPlatform {
    pluginConfiguration {
        id = 'sew'
        version = providers.gradleProperty("pluginVersion")
        name = 'Software Engineering Workbench (SEW)'
        description = 'Support for SEW inside IntelliJ'
        version = project.version

        ideaVersion {
            sinceBuild = "223"
            untilBuild = provider { (String) null }
        }

        vendor {
            name = 'org.setms'
            email = 'intellij@setms.org'
            url = 'https://www.setms.org'
        }
    }
}

generateLexer {
    sourceFile = file('src/main/grammars/Sew.flex')
    targetOutputDir = file('build/generated/sources/flex/org/setms/sew/intellij/language/sew')
    purgeOldFiles = true
}
sourceSets.main.java.srcDir 'build/generated/sources/flex'

generateParser {
    sourceFile = file('src/main/grammars/Sew.bnf')
    targetRootOutputDir = file('build/generated/sources/bnf')
    // These are required, but seem to be unused if the .bnf provides the FQNs
    pathToParser = 'parser'
    pathToPsiRoot = 'psi'
    purgeOldFiles = true
}
sourceSets.main.java.srcDir 'build/generated/sources/bnf'

compileJava.dependsOn generateParser, generateLexer